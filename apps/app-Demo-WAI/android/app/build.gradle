apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

/*
The ARCore aar library contains the native shared libraries. These are
extracted before building to a temporary directory.
*/
def arcore_libpath = "./arcore-native"
// Create a configuration to mark which aars to extract .so files from
configurations { natives }

android {
    signingConfigs {
        release {
            keyAlias 'cpvrlab'
            keyPassword 'uwenadat'
            storeFile file('../cpvrlab-release-key')
            storePassword 'uwenadat'
        }
    }
    compileSdkVersion 29
    //buildToolsVersion "29.0.1"
    defaultConfig {
        applicationId "ch.cpvr.wai"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                cppFlags "-frtti -fexceptions -std=c++14 -w -Wno-error=format-security"
                abiFilters 'arm64-v8a'
                arguments "-DANDROID_STL=c++_shared", "-DSL_BUILD_WAI=ON", "-DSL_APP=SL_APP_WAI"
            }
        }
    }
    sourceSets {
        main {
            assets.srcDirs = ['src/main/assets']
        }
        debug {
            // NOTE(jan): This directory MUST contain a subfolder called arm64-v8a (or whatever ABI is built) that contains the libraries
            jniLibs.srcDirs = [
                    '../../../../externals/prebuilt/andV8_g2o/Debug'
            ]
        }
        release {
            // NOTE(jan): This directory MUST contain a subfolder called arm64-v8a (or whatever ABI is built) that contains the libraries
            jniLibs.srcDirs = [
                    '../../../../externals/prebuilt/andV8_g2o/Release'
            ]
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            debuggable false
        }
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path file('../../../../CMakeLists.txt')
        }
    }

}

// Extracts the shared libraries from aars in the natives configuration.
// This is done so that NDK builds can access these libraries.
task extractNativeLibraries() {
    // Extract every time.
    outputs.upToDateWhen { false }

    doFirst {
        configurations.natives.files.each { f ->
            copy {
                from zipTree(f)
                into arcore_libpath
                include "jni/**/*"
            }
        }
    }
}

tasks.whenTaskAdded {
    task-> if (task.name.contains("external") && !task.name.contains("Clean")) {
        task.dependsOn(extractNativeLibraries)
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    implementation 'com.google.ar:core:1.20.0'
    natives 'com.google.ar:core:1.20.0'
}
